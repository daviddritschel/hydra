program rest

use constants

! Sets up initial conditions for a flow at rest.

implicit none
double precision:: uuha(1:nz),hhat(nz),kdsq(nz),kk0(nz),kkm(nz)
double precision:: qq(0:ny,0:nxm1,nz),qb(0:ny,0:nxm1)
double precision:: bety(0:ny),td,fac,noise_lvl
integer:: k,i,ix,iy,iz
integer, dimension(:), allocatable :: seed

!-----------------------------------------------------------------------
! Initialise random number generator:
call random_seed(size=k)
allocate(seed(1:k))
seed(:)=iseed
do i=1,iseed
  call random_seed(put=seed)
enddo

!--------------------------------------------------------------------
 !Read layer-mean zonal flow and project onto vertical modes:
open(60,file='meanflow.asc',status='old')
do iz=1,nz
   read(60,*) uuha(iz)
enddo
 !"ha" stands for "horizontal average" (i.e. over a layer).
close(60)

!--------------------------------------------------------------------
 !Read vertical structure and mode files (generated by vertical.f90):
open(60,file='vertical.asc',status='old')
do iz=1,nz
   read(60,*) hhat(iz),kdsq(iz)
enddo
close(60)
 !hhat = mean layer depth / total mean depth (sum(hhat) = 1).
 !kdsq = f^2/(b'*H) where f = Coriolis frequency, b' = buoyancy
 !       difference between layer iz and iz+1, H = total mean depth.
 !Note: kdsq(nz) is unused.

 !Ensure hhat sums to 1 - this is essential:
fac=1.d0/sum(hhat)
hhat=fac*hhat

!-----------------------------------------------------------------
 !For computing relative vorticity and implementing Ekman drag:
do iz=1,nz-1
   kk0(iz)=kdsq(iz)/hhat(iz)
   kkm(iz+1)=kdsq(iz)/hhat(iz+1)
enddo

! Define PV consistent with mean flow and make sure beta*y is included:
do iy=0,ny
   do ix=0,nxm1
      qq(iy,ix,1)=(beta+kk0(1)*(uuha(1)-uuha(2)))*(ymin+gly*dble(iy))
      do iz=2,nz-1
         qq(iy,ix,iz)=(beta+kk0(iz)*(uuha(iz)-uuha(iz+1)) &
                         & +kkm(iz)*(uuha(iz)-uuha(iz-1)))*(ymin+gly*dble(iy))
      enddo
      qq(iy,ix,nz)=(beta+kkm(nz)*(uuha(nz)-uuha(nz-1)))*(ymin+gly*dble(iy))
   enddo
enddo

! Get bathymetry
if (bath) then
   open(11,file='bath.r8',form='unformatted', &
        access='direct',status='old',recl=2*nhbytes)
   read(11,rec=1) td,qb
   close(11)

    do ix=0,nxm1
        do iy=0,ny
            qq(iy,ix,nz)=qq(iy,ix,nz)+qb(iy,ix)
        enddo
    enddo
endif

! Define PV and make sure beta*y is included:
write(*,*) ' Enter the noise level imposed on initial PV'
read(*,*) noise_lvl

! Add random noise to qq
if (abs(noise_lvl)>0.0) then
   do iz=1,nz
      do iy=0,ny
         do ix=0,nxm1
            call random_number(fac)
            qq(iy,ix,iz)=qq(iy,ix,iz)+noise_lvl*(2.0*fac-1.0)
         enddo
      enddo
   enddo
   write(*,*) ' Noise level imposed on initial PV = ',noise_lvl
endif

! Write PV distribution to a file:
open(20,file='qq_init.r8',form='unformatted', &
      access='direct',status='replace',recl=2*nbytes)
write(20,rec=1) zero,qq
close(20)

end program rest
