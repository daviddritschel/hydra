program zonal
!  -------------------------------------------------------------------------
!  |   Computes the zonal mean absolute vorticity, <zeta+f>, from data in  |
!  |   in zz.r4 (generated by spe).                                        |
!  |                                                                       |
!  |   Output is to the unformatted, direct-access file "avg.r4" which     |
!  |   contains single-precision numerical data of the form                |
!  |                            t, <zeta+f>                                |
!  |   with each record corresponding to a given time read from zz.r4      |
!  |                                                                       |
!  |   The file ext-zonal.asc and max-zonal.asc contains the abs max       |
!  |   values of <zeta+f>.                                                 |
!  -------------------------------------------------------------------------

use constants
implicit double precision(a-h,o-z)

real:: zz(ng,nt),zzz(ng)
real:: t,zzzmax
double precision:: cof(ng)

!---------------------------------------------------------------
 !Open input data file:
open(43,file='zz.r4',form='unformatted', &
    & access='direct',status='old',recl=nbytes)

 !Open output files:
open(22,file='avg.r4',form='unformatted', access='direct', &
                 &  status='replace',recl=4+4*ng)
open(51,file='ext-zonal.asc',status='replace')

!---------------------------------------------------------------
 !Define Coriolis frequency (f):
do j=1,ng
  cof(j)=fpole*sin((dble(j)-f12)*dl-hpi)
enddo

zfac=one/dble(nt)

!---------------------------------------------------------------
 !Read data and process:
loop=0
do  
  loop=loop+1
  iread=0
  read(43,rec=loop,iostat=iread) t,zz
  if (iread .ne. 0) exit 
  write(*,'(a,f9.2)') ' Processing t = ',t

   !Compute zonal averages:
  do j=1,ng
    zzz(j)=zero
  enddo

  do i=1,nt
    do j=1,ng
      zzz(j)=zzz(j)+zz(j,i)
    enddo
  enddo

  do j=1,ng
    zzz(j)=zzz(j)*zfac+cof(j)
  enddo

   !Write diagnostic data for this time:
  write(22,rec=loop) t,zzz

   !Find max abs values and write to a file:
  zzzmax=abs(zzz(1))
  do j=2,ng
    zzzmax=max(zzzmax,abs(zzz(j)))
  enddo
  write(51,'(f9.2,1x,f15.8)') t,zzzmax

enddo

 !Close all files:
close(43)
close(22)
close(51)

write(*,*)
write(*,*) ' The results are ready in avg.r4; to view the results type'
write(*,*) ' zonalview'
write(*,*)
write(*,*) ' The extremal values vs time are listed in ext-zonal.asc'

end program
