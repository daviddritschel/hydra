!#########################################################################
!  Finds the balanced fields from the conditions delta_t=gamma_t=0
!  using data previously generated by caps.

!           Written 5/4/2018 by D G Dritschel @ St Andrews
!#########################################################################

program dgbal

 !Import contants, parameters and common arrays:
use constants
use spectral

implicit none

 !Physical fields:
double precision:: qq(ng,ng),hh(ng,ng),dd(ng,ng),gg(ng,ng)
double precision:: uu(ng,ng),vv(ng,ng),zz(ng,ng)
double precision:: aa(ng,ng),bb(ng,ng),cc(ng,ng)
double precision:: htot(ng,ng),hx(ng,ng),hy(ng,ng),ht(ng,ng)
double precision:: ut(ng,ng),vt(ng,ng),ztn(ng,ng)
double precision:: wkg(ng,ng),wkh(ng,ng),wkm(ng,ng)
double precision:: wkp(ng,ng),wkq(ng,ng),wkz(ng,ng)
double precision:: badd(ng,ng)
double precision:: hhpre(ng,ng),ddpre(ng,ng),ggpre(ng,ng),zzpre(ng,ng)

 !Spectral fields:
double precision:: ds(ng,ng),gs(ng,ng),uds(ng,ng),vds(ng,ng)
double precision:: wka(ng,ng),wkb(ng,ng),wkc(ng,ng),wkd(ng,ng),wke(ng,ng)
double precision:: sds(ng,ng),sgs(ng,ng)

 !Other constants:
double precision,parameter:: tole=1.d-10
 !tole: relative energy norm error in successive iterates when finding
 !      hh, uu & vv from qq, dd & gg.  The energy error is computed from 
 !      <(u-u0)^2+(v-v0)^2+c^2*(h-h0)^2>/<u^2+v^2+c^2*h^2>
 !      where <:> means a domain average and (u0,v0,h0) is the previous
 !      guess for (u,v,h).
double precision:: uio,vio
double precision:: qadd,qbar,fqbar
double precision:: ddrmserr,ggrmserr,toterr,toterrpre
double precision:: eu
real:: tr4,qqr4(ng,ng)
integer:: loop,iread,iter

!----------------------------------------------------------------------
 !Initialise inversion constants and arrays:
call init_spectral

 !Open input data files:
open(31,file='qq.r4',form='unformatted',access='direct', &
                 & status='old',recl=nbytes)
open(32,file='dd.r4',form='unformatted',access='direct', &
                 & status='old',recl=nbytes)
open(33,file='gg.r4',form='unformatted',access='direct', &
                 & status='old',recl=nbytes)
open(34,file='hh.r4',form='unformatted',access='direct', &
                 & status='old',recl=nbytes)
open(35,file='zz.r4',form='unformatted',access='direct', &
                 & status='old',recl=nbytes)

 !Open output data files:
open(42,file='bdd.r4',form='unformatted',access='direct', &
                 & status='replace',recl=nbytes)
open(43,file='bgg.r4',form='unformatted',access='direct', &
                 & status='replace',recl=nbytes)
open(44,file='bhh.r4',form='unformatted',access='direct', &
                 & status='replace',recl=nbytes)
open(45,file='bzz.r4',form='unformatted',access='direct', &
                 & status='replace',recl=nbytes)
open(46,file='buu.r4',form='unformatted',access='direct', &
                 & status='replace',recl=nbytes)
open(47,file='bvv.r4',form='unformatted',access='direct', &
                 & status='replace',recl=nbytes)

open(51,file='bene.asc',status='replace')
open(61,file='berror.asc',status='replace')

!>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
 !Read data and process:
loop=0
do
  loop=loop+1
  iread=0
  read(31,rec=loop,iostat=iread) tr4,qqr4
  if (iread .ne. 0) exit 
  qq=dble(qqr4)

  read(32,rec=loop,iostat=iread) tr4,qqr4
  dd=dble(qqr4)
  read(33,rec=loop,iostat=iread) tr4,qqr4
  gg=dble(qqr4)
  read(34,rec=loop,iostat=iread) tr4,qqr4
  hh=dble(qqr4)
  read(35,rec=loop,iostat=iread) tr4,qqr4
  zz=dble(qqr4)

  write(*,'(a,f9.2)') ' *** Processing t = ',tr4

  !-----------------------------------------------------------------
  !Obtain velocity field from dd & zz:
  aa=dd
  call ptospc(ng,ng,aa,ds,xfactors,yfactors,xtrig,ytrig)
  wke=rlap*ds
  call xderiv(ng,ng,hrkx,wke,wka)
  call yderiv(ng,ng,hrky,wke,wkb)

  aa=zz
  call ptospc(ng,ng,aa,wke,xfactors,yfactors,xtrig,ytrig)
  wke=rlap*wke
  call xderiv(ng,ng,hrkx,wke,wkc)
  call yderiv(ng,ng,hrky,wke,wkd)

  wka=wka-wkd
  wkb=wkb+wkc
  call spctop(ng,ng,wka,uu,xfactors,yfactors,xtrig,ytrig)
  call spctop(ng,ng,wkb,vv,xfactors,yfactors,xtrig,ytrig)

  !Add mean flow (uio,vio):
  uio=-sum(hh*uu)*dsumi
  vio=-sum(hh*vv)*dsumi
  uu=uu+uio
  vv=vv+vio

  !-----------------------------------------------------------------
  !Initialise arrays for iteration below:
  aa=gg
  call ptospc(ng,ng,aa,gs,xfactors,yfactors,xtrig,ytrig)

  !-----------------------------------------------------------------
  !Iterate to find the SW balanced fields (corresponding to H = 0):

  !Energy norm error (must be > tole to start):
  toterrpre=one/small**2
  toterr=f12
  hhpre=hh
  ddpre=dd
  ggpre=gg
  zzpre=zz
  do while (toterr .gt. tole)
    !Obtain balanced estimate for gamma (gg):
    call jacob(uu,vv,wkp)
    call ptospc(ng,ng,wkp,wkb,xfactors,yfactors,xtrig,ytrig)
    wkp=dd*uu
    wkq=dd*vv
    call divs(wkp,wkq,wka)
    gs=filt*(wka-two*wkb)
    wka=gs
    call spctop(ng,ng,wka,gg,xfactors,yfactors,xtrig,ytrig)
    ggrmserr=sum((gg-ggpre)**2)/(sum(ggpre**2)+small)

    !Obtain balanced estimate for delta (dd):
    wkp=hh*uu
    wkq=hh*vv
    call divs(wkp,wkq,wka)
    wkp=zz*uu
    wkq=zz*vv
    call divs(wkp,wkq,wkb)
    ds=helm*(cof*wkb-c2g2*wka)
    wka=ds
    call spctop(ng,ng,wka,dd,xfactors,yfactors,xtrig,ytrig)
    ddrmserr=sum((dd-ddpre)**2)/(sum(ddpre**2)+small)

    !Find height anomaly field (hh):
    htot=one+hh
    qadd=-dsumi*sum(qq*htot)
    qq=qq+qadd
    qbar=dsumi*sum(qq)
    wkp=cof*(qq+hh*(qq-qbar))-gg
    call ptospc(ng,ng,wkp,wkb,xfactors,yfactors,xtrig,ytrig)
    fqbar=cof*qbar
    wka=filt*wkb/(opak-fqbar)
    call spctop(ng,ng,wka,hh,xfactors,yfactors,xtrig,ytrig)
     !wkp: corrected de-aliased height field (to be hh below)
    htot=one+hh

    !Obtain relative vorticity field (zz):
    wkp=qq*htot
    qadd=-dsumi*sum(wkp)
    qq=qq+qadd
    zz=htot*(cof+qq)-cof

    !Obtain velocity field (uu,vv):
    call ptospc(ng,ng,zz,wkb,xfactors,yfactors,xtrig,ytrig)
    wka=rlap*wkb
    wkb=filt*wkb
    call spctop(ng,ng,wkb,zz,xfactors,yfactors,xtrig,ytrig)
    call xderiv(ng,ng,hrkx,wka,wkd)
    call yderiv(ng,ng,hrky,wka,wkb)
    wke=rlap*ds
    call xderiv(ng,ng,hrkx,wke,wka)
    call yderiv(ng,ng,hrky,wke,wkc)
    wkb=wka-wkb
    wkd=wkc+wkd
    call spctop(ng,ng,wkb,uu,xfactors,yfactors,xtrig,ytrig)
    call spctop(ng,ng,wkd,vv,xfactors,yfactors,xtrig,ytrig)

    !Add mean flow (uio,vio):
    uio=-sum(hh*uu)*dsumi
    vio=-sum(hh*vv)*dsumi
    uu=uu+uio
    vv=vv+vio

    !Compute overall error:
    toterr=f12*(ddrmserr+ggrmserr)

    write(*,*) ' relative delta error = ',ddrmserr
    write(*,*) ' relative gamma error = ',ggrmserr

    !If error is going up again, stop and save fields:
    if (toterrpre .lt. toterr) exit

    !Otherwise continue with another iteration:
    hhpre=hh
    ddpre=dd
    ggpre=gg
    zzpre=zz
    toterrpre=toterr
  enddo

  write(*,*) ' Minimum error = ',toterrpre

  !-----------------------------------------------------------------
  !Iterate to find the GN balanced fields from SW balanced fields:

  !Energy norm error (must be > tole to start):
  toterrpre=one/small**2
  toterr=f12
  hhpre=hh
  ddpre=dd
  ggpre=gg
  zzpre=zz
  iter=1
  do while (toterr .gt. tole)
    !Obtain balanced estimate for gamma (gg):
    call jacob(uu,vv,wkp)
    call ptospc(ng,ng,wkp,wkc,xfactors,yfactors,xtrig,ytrig)
    hx=dd*uu
    hy=dd*vv
    call divs(hx,hy,wka)
    gs=filt*(wka-two*wkc)
    wka=gs
    call spctop(ng,ng,wka,gg,xfactors,yfactors,xtrig,ytrig)
    ggrmserr=sum((gg-ggpre)**2)/(sum(ggpre**2)+small)

    !Obtain balanced estimate for delta (dd):
    wka=gs+two*wkc
    call spctop(ng,ng,wka,aa,xfactors,yfactors,xtrig,ytrig)
    aa=aa-two*dd**2
    call dealias(aa)
    wkp=hh
    call ptospc(ng,ng,wkp,wka,xfactors,yfactors,xtrig,ytrig)
    call xderiv(ng,ng,hrkx,wka,wkd)
    call spctop(ng,ng,wkd,hx,xfactors,yfactors,xtrig,ytrig)
    call yderiv(ng,ng,hrky,wka,wkd)
    call spctop(ng,ng,wkd,hy,xfactors,yfactors,xtrig,ytrig)
    htot=one+hh
    wkp=hh*uu
    wkq=hh*vv
    call divs(wkp,wkq,wkd)
    wkd=filt*wkd
    wke=c2g2*wkd
    call spctop(ng,ng,wkd,ht,xfactors,yfactors,xtrig,ytrig)
    ht=-ht-dd
    wkz=zz+cof
    wkp=zz*uu
    wkq=zz*vv
    call divs(wkp,wkq,wkd)
    call spctop(ng,ng,wkd,ztn,xfactors,yfactors,xtrig,ytrig)
    call jacob(aa,hh,wkp)
    ztn=hbsq3*wkp*htot-ztn
    wkp=f12*(uu**2+vv**2)
    call ptospc(ng,ng,wkp,wka,xfactors,yfactors,xtrig,ytrig)
    wka=filt*wka
    call xderiv(ng,ng,hrkx,wka,wkd)
    call spctop(ng,ng,wkd,wkp,xfactors,yfactors,xtrig,ytrig)
    call yderiv(ng,ng,hrky,wka,wkd)
    call spctop(ng,ng,wkd,wkq,xfactors,yfactors,xtrig,ytrig)
    wkh=htot**2
    call dealias(wkh)
    wkm=htot*aa
    call dealias(wkm)
    wkh=wkh*wkm
    call ptospc(ng,ng,wkh,wka,xfactors,yfactors,xtrig,ytrig)
    wka=filt*wka
    call xderiv(ng,ng,hrkx,wka,wkd)
    call spctop(ng,ng,wkd,wkg,xfactors,yfactors,xtrig,ytrig)
    call yderiv(ng,ng,hrky,wka,wkd)
    call spctop(ng,ng,wkd,wkh,xfactors,yfactors,xtrig,ytrig)
    wkm=hbsq3/htot
    call dealias(wkm)
    ut=wkm*wkg-wkp+wkz*vv-csq*hx
    call dealias(ut)
    vt=wkm*wkh-wkq-wkz*uu-csq*hy
    call dealias(vt)
    call jacob(ut,vv,wkp)
    call jacob(uu,vt,wkq)
    wkp=wkp+wkq
    call dealias(wkp)
    bb=two*htot*wkp
    call ptospc(ng,ng,bb,wkb,xfactors,yfactors,xtrig,ytrig)
    wkb=filt*wkb
    call spctop(ng,ng,wkb,bb,xfactors,yfactors,xtrig,ytrig)
    call ptospc(ng,ng,ztn,wkb,xfactors,yfactors,xtrig,ytrig)
    wkp=ht*aa
    call dealias(wkp)
    wkp=htot*(two*wkp+bb)
    call ptospc(ng,ng,wkp,wka,xfactors,yfactors,xtrig,ytrig)
    wka=cof*wkb+wke-hbsq3*rksq*wka
    wkp=htot*ht
    call ptospc(ng,ng,wkp,wkc,xfactors,yfactors,xtrig,ytrig)
    wkc=filt*wkc
    call xderiv(ng,ng,hrkx,wkc,wkd)
    call spctop(ng,ng,wkd,wkg,xfactors,yfactors,xtrig,ytrig)
    call yderiv(ng,ng,hrky,wkc,wkd)
    call spctop(ng,ng,wkd,wkh,xfactors,yfactors,xtrig,ytrig)
    wkp=aa*wkg+bb*hx
    wkq=aa*wkh+bb*hy
    call divs(wkp,wkq,wkb)
    ds=-helm*(wka+hbsq3*wkb)
    wka=ds
    call spctop(ng,ng,wka,dd,xfactors,yfactors,xtrig,ytrig)
    ddrmserr=sum((dd-ddpre)**2)/(sum(ddpre**2)+small)

    !Find height anomaly field (hh):
    wkp=dd**2
    call dealias(wkp)
    badd=gg-two*wkp
    call jacob(hh,dd,cc)
    cc=hbsq3*cc
    call dealias(cc)
    qadd=-dsumi*sum(qq*htot)
    qq=qq+qadd
    qbar=dsumi*sum(qq)
    call jacob(uu,vv,wkp)
    call dealias(wkp)
    bb=hbsq3*htot*(badd+two*wkp)
    call dealias(bb)
    hx=bb*hx
    hy=bb*hy
    call divs(hx,hy,wka)
    wkp=htot*bb
    call ptospc(ng,ng,wkp,wkc,xfactors,yfactors,xtrig,ytrig)
    wkc=wka-rksq*wkc
    wkp=cof*(qq+hh*(qq-qbar)-htot*cc)-gg
    call ptospc(ng,ng,wkp,wkb,xfactors,yfactors,xtrig,ytrig)
    fqbar=cof*qbar
    wka=filt*(wkb+wkc)/(opak-fqbar)
    call spctop(ng,ng,wka,hh,xfactors,yfactors,xtrig,ytrig)
    htot=one+hh

    !Obtain relative vorticity field (zz):
    call jacob(hh,dd,cc)
    cc=hbsq3*cc
    call dealias(cc)
    wkp=qq*htot
    qadd=-dsumi*sum(wkp)
    qq=qq+qadd
    zz=htot*(cof+qq-cc)-cof

    !Obtain velocity field (uu,vv):
    call ptospc(ng,ng,zz,wkb,xfactors,yfactors,xtrig,ytrig)
    wka=rlap*wkb
    wkb=filt*wkb
    call spctop(ng,ng,wkb,zz,xfactors,yfactors,xtrig,ytrig)
    call xderiv(ng,ng,hrkx,wka,wkd)
    call yderiv(ng,ng,hrky,wka,wkb)
    wke=rlap*ds
    call xderiv(ng,ng,hrkx,wke,wka)
    call yderiv(ng,ng,hrky,wke,wkc)
    wkb=wka-wkb
    wkd=wkc+wkd
    call spctop(ng,ng,wkb,uu,xfactors,yfactors,xtrig,ytrig)
    call spctop(ng,ng,wkd,vv,xfactors,yfactors,xtrig,ytrig)

    !Add mean flow (uio,vio):
    uio=-sum(hh*uu)*dsumi
    vio=-sum(hh*vv)*dsumi
    uu=uu+uio
    vv=vv+vio

    !Compute overall error:
    toterr=f12*(ddrmserr+ggrmserr)

    write(*,*) ' relative delta error = ',ddrmserr
    write(*,*) ' relative gamma error = ',ggrmserr

    !If error is going up again, stop and save fields:
    if (toterrpre .lt. toterr) exit

    !Otherwise continue with another iteration:
    hhpre=hh
    ddpre=dd
    ggpre=gg
    zzpre=zz
    toterrpre=toterr

    iter=iter+1
  enddo

  write(*,*) ' Minimum error = ',toterrpre

  !Compute total balanced energy:
  eu=f12*garea*(sum(htot*(uu**2+vv**2+hbsq3*dd**2)+csq*hh**2))

  !-----------------------------------------------------------------
  !Write data:
  write(42,rec=loop) tr4,real(ddpre)
  write(43,rec=loop) tr4,real(ggpre)
  write(44,rec=loop) tr4,real(hhpre)
  write(45,rec=loop) tr4,real(zzpre)
  write(51,'(f9.2,1x,f16.9)') tr4,eu
  write(61,'(f9.2,1x,f16.9)') tr4,toterrpre
enddo
!<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<

 !Close files:
close(31)
close(32)
close(33)
close(34)
close(35)
close(42)
close(43)
close(44)
close(45)
close(51)
close(61)

write(*,*)
write(*,*) ' Balanced fields generated.'

 !End main program
end program
!=======================================================================
