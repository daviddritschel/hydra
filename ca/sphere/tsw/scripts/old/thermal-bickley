#!/bin/csh

#=========================================================================#
#   Job setup script for the case of a thermal equilibrium Bickley jet
#=========================================================================#
set datagen = "thermal-bickley"

# Set fixed constants:
set pi=3.14159265358979
set twopi=`echo "scale=10; 2 * $pi" | bc -l`

#==========================================================================
echo ' We set up a Bickley jet,  u(phi) = u_0 sech^2((phi-phi_0)/b),'
echo ' as the thermal equilibrium profile.'
echo -n ' Normalised maximum speed, u_0/2*pi (default 0.6)? '
set u0=$<
if ($u0 == "") set u0="0.6"
echo -n ' Jet core latitude, phi_0 (default pi/18)? '
set phi0=$<
if ($phi0 == "") set phi0=`echo "scale=12; $pi/18" | bc -l`
echo -n ' Jet half width, b (default 0.25)? '
set b=$<
if ($b == "") set b="0.25"

echo ' ' >> job_info
echo ' Bickley (sech^2) jet, u_max/2*pi:   ' $u0 >> job_info
echo ' Core latitude, phi_0:               ' $phi0 >> job_info
echo ' Jet half width, b:                  ' $b >> job_info

echo
echo ' We add a height perturbation of the form'
echo '    h_0 exp(-(lambda/alpha)^2-((phi-phi_c)/beta)^2).'
echo -n ' Maximum height anomaly, h_0 (default 0.005)? '
set h0=$<
if ($h0 == "") set h0="0.005"
echo -n ' Central latitude, phi_c (default pi/18)? '
set phic=$<
if ($phic == "") set phic=`echo "scale=12; $pi/18" | bc -l`
echo -n ' Anomaly half width in longitude, alpha (default  1/3)? '
set alpha=$<
if ($alpha == "") set alpha=`echo "scale=12; 1/3" | bc -l`
echo -n ' Anomaly half width in  latitude,  beta (default 1/15)? '
set beta=$<
if ($beta == "") set beta=`echo "scale=12; 1/15" | bc -l`

echo ' ' >> job_info
echo ' Gaussian height anomaly, amp. h_0:  ' $h0 >> job_info
echo ' Central latitude, phi_c:            ' $phic >> job_info
echo ' Longitudinal half width, alpha:     ' $alpha >> job_info
echo ' Latitudinal  half width,  beta:     ' $beta >> job_info

cat << /EOF > in_$datagen
$u0 $phi0 $b
$h0 $phic $alpha $beta
/EOF

$datagen < in_$datagen > out_$datagen
