#!/usr/bin/env csh

#=========================================================================#
#   Job setup script for an initial condition consisting of a pair of
#   Bickley jets centred on the equator
#=========================================================================#

set datagen = "doublejet"

# Default input parameters:
set phi0 = "20.0" # Jets are at latitudes +/-phi0 (degrees)
set b    = "5.0" # Jet half width (degrees)
set fr   = "0.5"  # Froude number at jet maximum
set a   = "0.01"  # Max. vort. pert. / Planetary ang. vel.
set p   = "30.0"    # Central latitude of vorticity perturbation
#=========================================================================#

echo ' We consider a planet of radius 1 rotating with a period of one "day".'
echo
echo ' We start with a pair of jets with angular velocity of the form'
echo ' Omega = Omega_0*[sech^2((phi-phi_0)/b) + sech^2((phi+phi_0)/b)]'
echo ' where Omega_0 = c*F_r/cos(phi_0), and F_r is the Froude number.'

echo
echo -n ' Enter phi_0 in degrees (default' $phi0'): ' 
set var=$<
if ($var != "") set phi0=$var
echo -n ' Enter b in degrees (default' $b'): '
set var=$<
if ($var != "") set b=$var
echo -n ' Enter the Froude number F_r (default' $fr'): '
set var=$<
if ($var != "") set fr=$var

echo
echo ' We add a vorticity perturbation of the form 2*pi*A*exp(-4*d^2/(4-d^2)),'
echo ' where d is the chord distance from a point at latitude,longitude (p,0).'
echo ' *** Note, the global vorticity perturbation mean is removed.'
echo
echo -n ' Enter A (default' $a'): ' 
set var=$<
if ($var != "") set a=$var
echo -n ' Enter p (default' $p'): '
set var=$<
if ($var != "") set p=$var

echo ' ' >> job_info
echo ' Jets are at phi = +/-phi_0;   phi_0:' $phi0 >> job_info
echo ' Jet half width:                     ' $b >> job_info
echo ' Jet Froude number:                  ' $fr >> job_info
echo ' Max. vort. pert./Planetary ang. vel:' $a >> job_info
echo ' Central latitude of vort. pert.:    ' $p >> job_info

cat << /EOF > in_$datagen
$phi0 $b $fr
$a $p
/EOF

$datagen < in_$datagen > out_$datagen
