program zonal
!  -------------------------------------------------------------------------
!  |   Computes the zonal mean zonal velocity u, <u>, height anomaly, <h>, |
!  |   eddy kinetic energy <h[(u-<u>)^2+v^2]>/2, zonal mean PV, <q>, and   |
!  |   eddy enstrophy <(q-<q>)^2>/2 as a function of latitude from data    |
!  |   in hh.r4, uu.r4, vv.r4 and zz.r4 (generated by spe caps).           |
!  |                                                                       |
!  |   Output is to the unformatted, direct-access file "avg.r4" which     |
!  |   contains single-precision numerical data of the form                |
!  |           t, <u>, <h>, <h[(u-<u>)^2+v^2]>/2, <q>                      |
!  |   with each record corresponding to a given time read from the input  |
!  |   files hh.r4 etc.                                                    |
!  |                                                                       |
!  |   The file ext-zonal.asc and max-zonal.asc contains the abs max       |
!  |   values of all of all of these fields, i.e. t, |<u>|_max, ....       |
!  -------------------------------------------------------------------------

use constants
implicit double precision(a-h,o-z)

real:: t,uu(ng,nt),vv(ng,nt),hh(ng,nt),zz(ng,nt)
real:: zuu(ng),zhh(ng),zek(ng),zqq(ng)
real:: zuumax,zhhmax,zekmax,zqqmax
double precision:: cof(ng),clat(ng)

!---------------------------------------------------------------
 !Open all input data files:
open(40,file='hh.r4',form='unformatted', &
    & access='direct',status='old',recl=nbytes)
open(41,file='uu.r4',form='unformatted', &
    & access='direct',status='old',recl=nbytes)
open(42,file='vv.r4',form='unformatted', &
    & access='direct',status='old',recl=nbytes)
open(43,file='zz.r4',form='unformatted', &
    & access='direct',status='old',recl=nbytes)

 !Open output files:
open(22,file='avg.r4',form='unformatted', access='direct', &
                 &  status='replace',recl=4+16*ng)
open(51,file='ext-zonal.asc',status='replace')

!---------------------------------------------------------------
 !Define Coriolis frequency (f):
do j=1,ng
  rlat=(dble(j)-f12)*dl-hpi
  clat(j)=cos(rlat)
  cof(j)=fpole*sin(rlat)
enddo

rsum=f1112*(clat(1)+clat(ng))
do j=2,ngm1
  rsum=rsum+clat(j)
enddo
 !Note, rsum = 1/sin(dl/2) - sin(dl/2)/6 exactly.
rsumi=one/rsum
dsumi=rsumi/dble(nt)
zfac=one/dble(nt)
hzfac=f12*zfac

!---------------------------------------------------------------
 !Read data and process:
loop=0
do  
  loop=loop+1
  iread=0
  read(40,rec=loop,iostat=iread) t,hh
  if (iread .ne. 0) exit 
  read(41,rec=loop,iostat=iread) t,uu
  read(42,rec=loop,iostat=iread) t,vv
  read(43,rec=loop,iostat=iread) t,zz
  write(*,'(a,f9.2)') ' Processing t = ',t

   !Compute zonal averages:
  do j=1,ng
    zuu(j)=zero
    zhh(j)=zero
    zek(j)=zero
    zqq(j)=zero
  enddo

  do i=1,nt
    do j=1,ng
      zuu(j)=zuu(j)+uu(j,i)
      zhh(j)=zhh(j)+hh(j,i)
      zqq(j)=zqq(j)+(cof(j)+zz(j,i))/(one+hh(j,i))
    enddo
  enddo

  do j=1,ng
    zhh(j)=zhh(j)*zfac
    zuu(j)=zuu(j)*zfac
    zqq(j)=zqq(j)*zfac
  enddo

  do i=1,nt
    do j=1,ng
      zek(j)=zek(j)+(one+hh(j,i))*((uu(j,i)-zuu(j))**2+vv(j,i)**2)
    enddo
  enddo

  do j=1,ng
    zek(j)=zek(j)*hzfac
  enddo

   !Write diagnostic data for this time:
  write(22,rec=loop) t,zuu,zhh,zek,zqq

   !Find max abs values and write to a file:
  zuumax=abs(zuu(1))
  zhhmax=abs(zhh(1))
  zekmax=abs(zek(1))
  zqqmax=abs(zqq(1))
  do j=2,ng
    zuumax=max(zuumax,abs(zuu(j)))
    zhhmax=max(zhhmax,abs(zhh(j)))
    zekmax=max(zekmax,abs(zek(j)))
    zqqmax=max(zqqmax,abs(zqq(j)))
  enddo
  write(51,'(f9.2,4(1x,f15.8))') t,zuumax,zhhmax,zekmax,zqqmax

enddo

 !Close all files:
close(40)
close(41)
close(42)
close(43)

close(22)
close(51)

write(*,*)
write(*,*) ' The results are ready in avg.r4; to view the results type'
write(*,*) ' zonalview'
write(*,*)
write(*,*) ' The extremal values vs time are listed in ext-zonal.asc'

end program
