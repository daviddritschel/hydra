#!/bin/csh

#========================================================================
# Generates a grayscale image in a chosen format from a selected character
# image file (ending in .c2) generated e.g. by r4toc2.f90

#   ===> Run this in the working directory containing the image <===
#========================================================================

# Specify the FORTRAN compiler together with compiler options:
alias f90 "gfortran -O3 -mcmodel=medium"

# Default number of bytes representing a single character:
set n_b_p_c="1"

# Look for c2 files in current working directory
set c2files=`/bin/ls *.c2`
set nf=$#c2files
# Choose one prefix as the default:
set infile_prefix = `echo $c2files[$nf] | awk -F. '{print $(NF-1)}'`

# Default input parameters to c2toppm.f90:
set nx     = "256"   # Horizontal image dimension
set ny     = "256"   #  Vertical  image dimension
set irec   = "1"     # Frame in character image file to convert to image
set format = "png"   # Set default output image format
#========================================================================
# Get home directory from a script:
set local_home=${HOME}
# Location of the hydra script directory:
set bindir=$local_home/hydra/scripts

echo
echo '          --------------------------------------------'
echo '          |  Character map -> image conversion tool  |'
echo '          --------------------------------------------'
echo

echo -n ' Prefix (before .c2) of the character map file (default:' $infile_prefix')? '
set var=$<
if ($var != "") set infile_prefix=$var

# Check that file exists:
if (!(-e $infile_prefix.c2)) then
   echo ' *** File does not exist!!!  Exiting.'
   exit(-1)
endif

echo
echo -n ' Horizontal dimension of file (default:' $nx')? '
set var=$<
if ($var != "") set nx=$var

echo -n '   Vertical dimension of file (default:' $ny')? '
set var=$<
if ($var != "") set ny=$var

echo -n ' Frame or record to image (default:' $irec')? '
set var=$<
if ($var != "") set irec=$var

echo -n ' Output image format (default:' $format')? '
set var=$<
if ($var != "") set format=$var


#========================================================================
# Use C pre-processor to put chosen parameters $copts into c2topgm.f90:
set copts="-DN_X=$nx -DN_Y=$ny -DN_BYTES_PER_CHAR=$n_b_p_c"
/bin/cp $bindir/graphics/c2topgm.f90 .
precomp $copts c2topgm.f90

# Compile programme:
f90 -o c2topgm c2topgm.f90

# Create input data file:
cat << /EOF > input_c2topgm
$infile_prefix
$irec
/EOF

# Run programme (creates a PGM file):
c2topgm < input_c2topgm > junk

# Extract pgm filename:
set pgmfile = `cat pgmfilename`

# Get prefix of filename:
set basename = `echo $pgmfile | awk -F. '{print $(NF-1)}'`

convert $basename.pgm $basename.$format
echo
echo ' Created' $basename.$format

# Clean up unwanted files:
/bin/rm $pgmfile pgmfilename input_c2topgm junk c2topgm*
