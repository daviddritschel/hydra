program enstrophu
!  -------------------------------------------------------------------------
!  |   Computes the enstrophy <q^2>/2 on the ultra-fine grid from data     |
!  |   previously generated by genfg.f90                                   |
!  -------------------------------------------------------------------------

use constants
implicit none

real:: tr4,zzr4(ngu,ntu)
double precision:: zz(ngu,ntu),clat(ngu)
double precision:: dlu,rsum,rsumi,dsumi,ens
integer:: i,j,period
character(len=3):: pind

!---------------------------------------------------------------
 !Define cos(lat):
dlu=twopi/dble(ntu)
do j=1,ngu
  clat(j)=cos((dble(j)-f12)*dlu-hpi)
enddo

 !For computing surface integrals:
rsum=f1112*(clat(1)+clat(ngu))
do j=2,ngu-1
  rsum=rsum+clat(j)
enddo
 !Note, rsum = 1/sin(dlu/2) - sin(dlu/2)/6 exactly.
rsumi=one/rsum
dsumi=rsumi/dble(ntu)

!---------------------------------------------------------------
 !Select time frame:
write(*,*) ' Time period (choose 0 for the initial one)?'
read(*,*) period
write(pind(1:3),'(i3.3)') period

 !Open input data file:
open(44,file='fine/qq'//pind//'.r4',form='unformatted',status='old')

 !Read data:
read(44) tr4,zzr4
close(44)
write(*,'(a,f12.5)') ' t = ',tr4

 !Convert to double precision:
zz=dble(zzr4)

 !Compute enstrophy:
ens=zero
do i=1,ntu
  ens=ens+f1112*(zz(1,i)**2*clat(1)+zz(ngu,i)**2*clat(ng))
  do j=2,ngu-1
    ens=ens+zz(j,i)**2*clat(j)
  enddo
enddo
ens=f12*dsumi*ens

write(*,'(a,f14.9)') ' <q^2>/2 = ',ens


end program
