#!/usr/bin/env csh

#=========================================================================#
#   Job setup script for the thermal relaxation case
#=========================================================================#

set datagen = "relax"

# Default input parameters:
set phis = "-20.0" # Latitude where h_e increases fastest (degrees)
set phin = "40.0"  # Latitude where h_e decreases fastest (degrees)
set b    = "10.0"  # Half width of height variation (degrees)
set eps  = "0.5"   # Equator - pole difference in height field
set a   = "0.1"    # Amplitude of latitude shift
set m   = "5"      # Longitudinal wavenumber of perturbation
#=========================================================================#

echo ' We consider the dimensionless thermal equilibrium height field'
echo ' h_e = H_e/H_char'
echo '     = C + 0.5*eps*[tanh((phi-phi_s)/b)-tanh((phi-phi_n)/b)]'
echo ' whose equator - pole difference is eps.  The constant C is'
echo ' chosen to ensure that the mean value of h_e is 1.'
echo ' The main increase in h_e occurs around the latitudes phi_s'
echo ' and phi_n, and occurs over a half width of b degrees.'
echo
echo -n ' Enter pole - equator height difference (default' $eps'): '
set var=$<
if ($var != "") set eps=$var
echo -n ' Enter phi_s in degrees (default' $phis'): ' 
set var=$<
if ($var != "") set phis=$var
echo -n ' Enter phi_n in degrees (default' $phin'): ' 
set var=$<
if ($var != "") set phin=$var
echo -n ' Enter b in degrees (default' $b'): '
set var=$<
if ($var != "") set b=$var

echo ' To induce instability, we displace the latitudes by the function'
echo ' A*cos(phi)*cos(m*lambda), where lambda is the longitude.'
echo
echo -n ' Enter the amplitude A (default' $a'): ' 
set var=$<
if ($var != "") set a=$var
echo -n ' Enter the wavenumber m (integer, default' $m'): '
set var=$<
if ($var != "") set m=$var

echo ' ' >> job_info
echo ' Eq-Pole height difference, epsilon: ' $eps >> job_info
echo ' Rise latitude of h_e, phi_s:        ' $phis >> job_info
echo ' Fall latitude of h_e, phi_n:        ' $phin >> job_info
echo ' Half width b (degrees):             ' $b >> job_info
echo ' ' >> job_info
echo ' Latitudes displaced by A*cos(phi)*cos(m*lambda)' >> job_info
echo ' Amplitude, A:                       ' $a >> job_info
echo ' Longitudinal wavenumber, m:         ' $m >> job_info

cat << /EOF > in_$datagen
$eps
$phis
$phin
$b
$a
$m
/EOF

$datagen < in_$datagen > out_$datagen
